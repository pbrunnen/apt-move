#!/usr/bin/awk -f
# $Id: pkg1,v 1.5 2003/02/22 10:56:33 herbert Exp $

function read(err) {
	err = getline < overf
	if (err < 0) {
		print "getline failed on " overf > "/dev/stderr"
		exit 1
	} else if (err == 0) {
		eof = 1
		return
	}

	opkg = $1
	oprio = $2 != "-" ? $2 : ""
	osect = $3 != "-" ? $3 : ""
	if (NF >= 5) {
		otask = $4 != "-" ? $4 : ""
	}
}

BEGIN {
	overf = ARGV[1]
	contents = ARGV[2]
	ARGC = 1

	read()
}

{
	pkg = $1
	file = $2
	while (!eof && opkg < pkg) {
		read()
	}

	task = sect = prio = ""
	if (!eof && opkg == pkg) {
		prio = oprio
		sect = osect
		task = otask
	}

	while ((err = getline < file) > 0) {
		if (/^Section:/) {
			if (sect != "") {
				print "Section: " sect
			} else {
				print
				sect = substr($0, 8)
				gsub(/ */, "", sect)
			}
			continue
		}
		if (prio != "" && /^Priority:/) {
			print "Priority: " prio
			continue
		}
		if (task != "" && /^$/) {
			print "Task: " task
		}
		print
	}

	if (err < 0) {
		print "getline failed on " file > "/dev/stderr"
		exit 1
	}

	close(file)
}

contents {
	cnt = split(file, a, "/")
	file = substr(file, 1, length(file) - length(a[cnt]) - 8)
	file = file "/.content/" a[cnt]
	while (getline < file > 0) {
		print $0 " " sect "/" pkg > contents
	}
	close(file)
}
